set(
    FORMAT_PATTERNS
    src/*.cpp src/*.hpp  src/*.c src/*.h
    include/*.hpp include/*.h
    test/*.cpp test/*.hpp test/*.c test/*.h
    benchmark/*.cpp benchmark/*.hpp benchmark/*.c benchmark/*.h
    example/*.cpp example/*.hpp example/*.c example/*.h
    apps/*.cpp apps/*.hpp apps/*.c apps/*.h
    apps/*/*.cpp apps/*/*.hpp apps/*/*.c apps/*/*.h
    CACHE STRING
    "; separated patterns relative to the project source dir to format"
)

set(FORMAT_COMMAND clang-format CACHE STRING "Formatter to use")

add_custom_target(
    format-check
    COMMAND "${CMAKE_COMMAND}"
    -D "FORMAT_COMMAND=${FORMAT_COMMAND}"
    -D "PATTERNS=${FORMAT_PATTERNS}"
    -P "${PROJECT_SOURCE_DIR}/cmake/lint.cmake"
    WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}"
    COMMENT "Linting the code"
    VERBATIM
)

add_custom_target(
    format-fix
    COMMAND "${CMAKE_COMMAND}"
    -D "FORMAT_COMMAND=${FORMAT_COMMAND}"
    -D "PATTERNS=${FORMAT_PATTERNS}"
    -D FIX=YES
    -P "${PROJECT_SOURCE_DIR}/cmake/lint.cmake"
    WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}"
    COMMENT "Fixing the code"
    VERBATIM
)
